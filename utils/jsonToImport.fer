# Emojis JSON file comes from https://gist.githubusercontent.com/oliveratgithub/0bf11a9aff0d6da7b46f1490f86a71eb/raw/d8e4b78cfe66862cf3809443c1dba017f37b61db/emojis.json

let io = import('std/io');
let fs = import('std/fs');
let map = import('std/map');
let fecl = import('std/fecl');
let json = import('std/json');
let logging = import('std/logging');

logging.addTarget(io.stdout, logging.Levels.INFO);

logging.info('Loading emojis json file ...');
let jsonStr = fs.fopen(__SRC_DIR__ / 'emojis.json', 'r').readAll();
let data = json.loads(jsonStr);

logging.info('Generating Feral import ...');

let scriptBoilerPlate = "
let map = import('std/map');

let Emoji = struct(
    category = '',
    name = '',
    emoji = ''
);

let emojis = map.new(refs = true);

";

let scriptTemplate = "emojis.insert('$<<shortname>>', Emoji('$<<item.category>>', '$<<item.name>>', '$<<item.emoji>>'));";

let outFile = fs.fopen(__SRC_DIR__ / '..' / 'include' / 'emojiMap.fer', 'w+');
io.fprintln(outFile, scriptBoilerPlate);

let sorter = fn(itemA, itemB) { return itemA.shortname < itemB.shortname; };
data['emojis'].sort(sorter);
for item in data['emojis'].each() {
    if item.shortname.empty() {
        logging.warn('Empty shortname for emoji: "', item.emoji, '", skipping it');
        continue;
    }
    item.erase('html');
    item.erase('order');
    item.erase('unicode');
    let shortname = item.shortname.replace(':', '');
    item.erase('shortname');
    io.fprintln(outFile, scriptTemplate.fmt());
}

logging.info('Finished!');